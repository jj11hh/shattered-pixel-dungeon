plugins {
    id 'java'
    id 'org.gretty' version '3.1.0'
}

gretty {
    contextPath = '/'
    extraResourceBase 'build/dist/webapp'
}

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
project.ext.mainClassName = 'com.shatteredpixel.shatteredpixeldungeon.teavm.TeaVMBuilder'

dependencies {
    implementation project(':core')

    implementation "com.github.xpenatan.gdx-teavm:gdx-freetype-teavm:$gdxTeaVMVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.github.xpenatan.gdx-teavm:backend-teavm:$gdxTeaVMVersion"
}

task buildJavaScript(dependsOn: classes, type: JavaExec) {
    setDescription("Transpile bytecode to JavaScript via TeaVM")
    mainClass.set(project.ext.mainClassName)
    setClasspath(sourceSets.main.runtimeClasspath)
}
build.dependsOn buildJavaScript

task run(dependsOn: [buildJavaScript, ":teavm:jettyRun"]) {
    setDescription("Run the JavaScript application hosted via a local Jetty server at http://localhost:8080/")
}